#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	//Проверка Курса
	МаксимальныйКурс = Константы.КоличествоКурсовУниверситета.Получить();
	
	Если Курс > МаксимальныйКурс Тогда
	
		Сообщение = Новый СообщениеПользователю();
		
		Сообщение.Текст = СтрШаблон("Ошибка! Указанный курс выше максимального - %1", МаксимальныйКурс);
		Сообщение.Поле = "Объект.Курс";
		
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
		
	//Создание ВТ со студентами
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ТЧСтуденты.НомерСтроки,
	|	ТЧСтуденты.Студент КАК Студент,
	|	ТЧСтуденты.Староста,
	|	ТЧСтуденты.ФормаОбучения,
	|	ТЧСтуденты.СуммаОбучения
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	&ТЧСтуденты КАК ТЧСтуденты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Студент";
	
	Запрос.УстановитьПараметр("ТЧСтуденты", Студенты);
	
	Запрос.Выполнить();
	
	//Проверка дублей студентов в ТЧ
	
	ЗапросДублейСтудентов = Новый Запрос;
	ЗапросДублейСтудентов.МенеджерВременныхТаблиц = МенеджерВТ;
	
	ЗапросДублейСтудентов.Текст = "ВЫБРАТЬ
	|	ДанныеДокумента.Студент
	|ПОМЕСТИТЬ ДублиСтудентов
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокумента.Студент
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(ДанныеДокумента.Студент) > 1
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДублиСтудентов.Студент КАК Студент,
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ДублиСтудентов КАК ДублиСтудентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеДокумента КАК ДанныеДокумента
	|		ПО ДублиСтудентов.Студент = ДанныеДокумента.Студент
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Студент";
	
	ВыборкаДублей = ЗапросДублейСтудентов.Выполнить().Выбрать();
	
	Пока ВыборкаДублей.Следующий() Цикл
		
		Сообщение = Новый СообщениеПользователю();
		
		Сообщение.Текст = СтрШаблон("Студент %1 встречается в документе более одного раза!", 																				ВыборкаДублей.Студент);
		Сообщение.Поле 	= СтрШаблон("Студенты[%1].Студент", ВыборкаДублей.НомерСтроки - 1);
		
		Сообщение.ПутьКДанным 	= "Объект";
		Сообщение.КлючДанных 	= Ссылка;
		
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЦикла;
	
	// Проверка нескольких старост
																					
	ЗапросСтаросты = Новый Запрос;
	ЗапросСтаросты.МенеджерВременныхТаблиц = МенеджерВТ;
	
	ЗапросСтаросты.Текст = "ВЫБРАТЬ
	|	ДанныеДокумента.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Студент КАК Студент
	|ИЗ
	|	ДанныеДокумента КАК ДанныеДокумента
	|ГДЕ
	|	ДанныеДокумента.Староста = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Студент";
	
	ВыборкаСтарост = ЗапросСтаросты.Выполнить().Выбрать();
	
	Если ВыборкаСтарост.Количество() > 1 Тогда
		
		Пока ВыборкаСтарост.Следующий() Цикл
			
			Сообщение = Новый СообщениеПользователю();
			
			Сообщение.Текст = СтрШаблон("В группе может быть только один староста!");
			Сообщение.Поле	= СтрШаблон("Студенты[%1].Староста", ВыборкаСтарост.НомерСтроки - 1);
			
			Сообщение.ПутьКДанным 	= "Объект";
			Сообщение.КлючДанных	= Ссылка;
			
			Сообщение.Сообщить();
			
			Отказ = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры



Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Учащиеся.Очистить();
	Движения.Взаиморасчеты.Очистить();
    Движения.Учащиеся.Записывать = Истина;
	Движения.Взаиморасчеты.Записывать = Истина;
    Движения.Записать();

	//Запрос с табличной частью
	МенеджерВТ = Новый МенеджерВременныхТаблиц();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ПриказОЗачисленииСтуденты.НомерСтроки,
	|	ПриказОЗачисленииСтуденты.Студент КАК Студент,
	|	ПриказОЗачисленииСтуденты.Староста,
	|	ПриказОЗачисленииСтуденты.ФормаОбучения,
	|	ПриказОЗачисленииСтуденты.СуммаОбучения
	|ПОМЕСТИТЬ Студенты
	|ИЗ
	|	Документ.ПриказОЗачислении.Студенты КАК ПриказОЗачисленииСтуденты
	|ГДЕ
	|	ПриказОЗачисленииСтуденты.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Студент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Учащиеся.Студент,
	|	Учащиеся.Группа
	|ПОМЕСТИТЬ СтудентыЧислятся
	|ИЗ
	|	РегистрСведений.Учащиеся КАК Учащиеся
	|ГДЕ
	|	Учащиеся.Студент В
	|		(ВЫБРАТЬ
	|			Студенты.Студент
	|		ИЗ
	|			Студенты КАК Студенты)
	|	И НЕ Учащиеся.Регистратор = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Студенты.НомерСтроки,
	|	Студенты.Студент,
	|	Студенты.ФормаОбучения,
	|	Студенты.Староста,
	|	ЕСТЬNULL(СтудентыЧислятся.Группа, НЕОПРЕДЕЛЕНО) КАК Группа,
	|	Студенты.СуммаОбучения
	|ИЗ
	|	Студенты КАК Студенты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СтудентыЧислятся КАК СтудентыЧислятся
	|		ПО Студенты.Студент = СтудентыЧислятся.Студент";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	ВыборкаСтудентов = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСтудентов.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаСтудентов.Группа) Тогда
			
			// Движения в РС Учащиеся
			Движение 				= Движения.Учащиеся.Добавить();
	        Движение.Период 		= Дата;
	        Движение.Факультет 		= Факультет;
	        Движение.Группа			= УчебнаяГруппа;
			Движение.Курс			= Курс;
			Движение.Студент		= ВыборкаСтудентов.Студент;
			Движение.Староста		= ВыборкаСтудентов.Староста;
			Движение.ФормаОбучения	= ВыборкаСтудентов.ФормаОбучения;
			
			//Движения в РН Взаиморасчеты
			Движение 				= Движения.Взаиморасчеты.ДобавитьПриход();
			Движение.ВидДвижения 	= ВидДвиженияНакопления.Приход;
			Движение.Период 		= Дата;
			Движение.Студент 		= ВыборкаСтудентов.Студент;
			Движение.Сумма 			= ВыборкаСтудентов.СуммаОбучения;
		
		Иначе
			
			Сообщение = Новый СообщениеПользователю();
			
			Сообщение.Текст = СтрШаблон("Студент %1 уже числиться группе %2",
																		ВыборкаСтудентов.Студент,
																		ВыборкаСтудентов.Группа);
			Сообщение.Поле = СтрШаблон("Студенты[%1].Студент", ВыборкаСтудентов.НомерСтроки - 1);
			
			Сообщение.ПутьКДанным 	= "Объект";
			Сообщение.КлючДанных 	= Ссылка;
			
			Сообщение.Сообщить();
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.Учащиеся.Записывать = Истина;
	Движения.Взаиморасчеты.Записывать = Истина;
	
КонецПроцедуры

#КонецОбласти