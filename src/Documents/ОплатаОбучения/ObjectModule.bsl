#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Взаиморасчеты.Очистить();
	
	Движение = Движения.Взаиморасчеты.ДобавитьРасход();
	
	Движение.Период 		= Дата;
	Движение.ВидДвижения	= ВидДвиженияНакопления.Расход;
	Движение.Студент		= Студент;
	Движение.Сумма			= Сумма;
	
	Движения.Взаиморасчеты.Записывать = Истина;
	Движения.Записать();
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	|	ВзаиморасчетыОстатки.Студент,
	|	-ВзаиморасчетыОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.Взаиморасчеты.Остатки(&МоментВремени, Студент = &Студент) КАК ВзаиморасчетыОстатки
	|ГДЕ
	|	ВзаиморасчетыОстатки.СуммаОстаток < 0";
	
	ГраницаКонтроля = Новый Граница(МоментВремени(), ВидГраницы.Включая);
    Запрос.УстановитьПараметр("МоментВремени", ГраницаКонтроля);
	Запрос.УстановитьПараметр("Студент", Студент);
    РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда
		
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		Сообщение = Новый СообщениеПользователю();
		
		Сообщение.Текст = СтрШаблон("Нельзя списать %1 рублей, отрицательный остаток - %2", 
																					Сумма, 
																					Выборка.СуммаОстаток);
		Сообщение.Поле = "Сумма";
		
		Сообщение.ПутьКДанным 	= "Объект";
		Сообщение.КлючДанных	= Ссылка;
		
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти
